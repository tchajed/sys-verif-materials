(* ONLY-WEB *)
(*| ---
category: lecture-note
tags: literate
order: 2
pageInfo: ["Date", "Category", "Tag", "Word"]
---
|*)
(* /ONLY-WEB *)
(*| # Introduction to Rocq

> Follow these notes in Rocq at [src/sys_verif/notes/rocq_intro.v](https://github.com/tchajed/sys-verif-fa25-proofs/blob/main/src/sys_verif/notes/rocq_intro.v).

In this lecture, we'll introduce Rocq as a system, functional programming, and
proving theorems about functional programs. This lecture is intended to follow Software Foundation's Basics chapter, so I recommend reading that first.

## Learning outcomes

By the end of this lecture, you should be able to

1. Interact with Rocq
2. Implement functions with pattern matching and recursion
3. Prove simple theorems about functions

|*)

(*|
## Rocq as an interactive theorem prover

Rocq is a lot like a programming language, but it is fundamentally _interactive_
in a way that is unlike programming languages you've used. The interaction is
necessary to write theorems, but understanding the interaction model is an
important part of how you will write definitions, find already proven lemmas,
and debug type errors.

::: info Rocq vs Coq

Rocq was formerly called Coq, with the name change implemented around January 2025. See the [note on the Rocq website](https://rocq-prover.org/about#Name). You will still see references to Coq, in particular in Software Foundations.

:::

|*)


(*|

## Functional programming

To write functional programs, we'll start by defining some data types for
our functions to operate on. |*)

(*| This is an "enumerated type". It defines `day`, and seven constructors for
that type. |*)
Inductive day : Type :=
| monday
| tuesday
| wednesday
| thursday
| friday
| saturday
| sunday.

(*| Now what we have `day`, we can define functions on days: |*)
(** next_weekday is a simple example of a function operating on [day] *)
Definition next_weekday (d: day) : day :=
  match d with
  | monday => tuesday
  | tuesday => wednesday
  | wednesday => thursday
  | thursday => friday
  | friday => monday
  | saturday => monday
  | sunday => monday
  end.

(*| Rocq has a number of commands for interacting with the system while it's
running. The first one we'll see is `Compute` below, which allows us to manually
check the behavior of the function we just defined. |*)
Compute (next_weekday friday). (* {OUTPUT} *)

(*| The main use of Rocq is to prove theorems - it is a proof assistant after
all. We'll get to more interesting theorems shortly, but for now let's prove a
"unit test" theorem.

NOTE: Theorem/Lemma/Example are all synonyms. In this class we'll try to stick
to Lemma.
|*)
Lemma next_weekday_test : next_weekday (next_weekday friday) = tuesday.
Proof.
  simpl. (* {GOAL} *)
  reflexivity.
Qed.


(*|

Now that we've seen some use of Rocq, let's take a step and characterize what we've seen.

Rocq has three programming languages, interwoven in one proof system: terms, vernacular, and tactics.

**Terms**: The Calculus of Inductive Constructions is the theory behind the term language. Due to the use of _dependent types_, there is no distinction between terms and types; it's all part of the same term language.
**Vernacular**: Vernacular is a sequence of stateful commands. They create definitions, change attributes. They can also be queries which don't affect the state but help you write code. When you use Rocq interactively, you've executed a prefix of the vernacular commands. You can move forward and backward, undoing commands. Vernacular commands create new types, definitions, and start proofs.
**Tactics** are used to prove theorems. This is yet another language. A proof consists of a number of goals, and tactics transform one goal into another (potentially solving the goal, or creating multiple new goals), hopefully making progress towards a finished proof. Once a theorem is proven, you can generally ignore how it was proven.

When you are done with a development, you generally re-run Rocq in "batch mode"
like a compiler, which runs the same vernacular commands and produces a compiled
output file. This is needed to make sure everything gets checked, and because
Rocq uses those outputs when it needs to import another file.

Unlike most programming languages (where you could work in a terminal, write code for a while, run the compiler or the program, and then repeat), you must interact with the system via the IDE frequently to develop a proof. This is because when you make progress in a proof, Rocq has an interface to show you the effect of that progress (that is, the remaining goal), and you will need to see that to decide what to do next. It is important for learning Rocq to both have _some_ mental model for what tactics to do and also to _read_ the resulting goals.

|*)

(*| ## Booleans and the usual functions |*)
Module BooleanPlayground.

Inductive bool : Type :=
| true
| false.

Definition negb (b: bool) : bool :=
  match b with
  | true => false
  | false => true
  end.
Definition andb (b1: bool) (b2: bool) : bool :=
  match b1 with
  | true => b2
  | false => false
  end.
Definition orb (b1:bool) (b2:bool) : bool :=
  match b1 with
  | true => true
  | false => b2
  end.

Lemma test_orb1: (orb true false) = true.
Proof. simpl. reflexivity. Qed.
Lemma test_orb2: (orb false false) = false.
Proof. simpl. reflexivity. Qed.
Lemma test_orb3: (orb false true) = true.
Proof. simpl. reflexivity. Qed.
Lemma test_orb4: (orb true true) = true.
Proof. simpl. reflexivity. Qed.

(*| Note `if` is an _expression_ and not a _statement_ (there are no
statements). Like Rust but not C or Go. Python has both (`if:` vs `e1 if b else
e2`). |*)

Definition negb' (b: bool) : bool :=
  if b then false else true.
Definition andb' (b1 b2: bool) : bool :=
  if b1 then b2 else false.

(*| Note on `if`: since booleans aren't built-in, and we just defined `bool`
above, Rocq's `if` expression works for any type with two constructors. |*)

(*| Just to convince you `andb'` has the same behavior as `andb` above. |*)
Lemma andb'_eq_andb : forall b1 b2, andb' b1 b2 = andb b1 b2.
Proof.
  intros b1 b2.
  (* this proof is not important right now *)
  destruct b1, b2; simpl; reflexivity.
Qed.

(*| ### In-class exercise: decoding type errors

Think about these errors on your own and try to explain how they were
produced. What is needed to fix each?

*)

Fail Definition simple1 (b: bool) : bool :=
  match b with
  | true => false
  | false => 0
  end.
(*
Error:
In environment
b : bool
The term "0" has type "nat" while it is expected to have type "bool".
*)

Fail Definition simple2 (b: bool) :=
  match b with
  | true => 0
  | false => false
  end.
(*
Error:
In environment
b : bool
The term "false" has type "bool" while it is expected to have type "nat".
*)

Fail Definition complex_expr1 (b1 b2 b3: bool) :=
  orb (andb' b2 false) (andb (orb (b1)) (b3)) b2.
(*
Error:
In environment
b1 : bool
b2 : bool
b3 : bool
The term "orb b1" has type "bool -> bool" while it is expected to have type "bool".
 *)

Fail Definition complex_expr2 b1 b2 b3 :=
  andb (andb b1 (b2 (orb b3 b1))) b2.
(*
Error:
In environment
b1 : bool
b2 : bool -> bool
b3 : bool
The term "b2" has type "bool -> bool" while it is expected to have type "bool".
*)

Fail Definition complex_expr3 b1 b2 b3 :=
  andb (andb b1 (orb b2 b3 b1)) b2.
(*
Error:
Illegal application (Non-functional construction):
The expression "orb b2 b3" of type "bool" cannot be applied to the term
 "b1" : "bool"
*)

End BooleanPlayground.

(*| ## Proof strategy

We'll do another exercise to get you thinking about how to approach a proof.

You will always have two challenges in completing a proof in this class: (1) why
is the theorem true?, and (2) how do we turn that into a Rocq proof?. It's
extremely helpful to understand the distinction to be able to develop these
skills independently.

|*)

(*| Let's go back to our `day` type. |*)
Definition next_day (d: day) : day :=
  match d with
  | monday => tuesday
  | tuesday => wednesday
  | wednesday => thursday
  | thursday => friday
  | friday => saturday
  | saturday => sunday
  | sunday => monday
  end.

(*| We can use case analysis to do proofs. |*)
Definition is_weekend (d: day) :=
  d = saturday \/ d = sunday.

Lemma weekend_next_day_weekend (d: day) :
  d = friday \/ d = saturday ->
  is_weekend (next_day d).
Proof.
(* EXERCISE: Admitted. *)
(* SOLUTION *)
  intros H.
  unfold is_weekend.
  destruct H.
  - subst. simpl. left. reflexivity.
  - subst. simpl. right. reflexivity.
Qed.
(* /SOLUTION *)


(*| Proving an [exists] is complicated and we'll have more to say, but try to
think through this intuitively for now. |*)
Lemma wednesday_has_prev_day : exists d, next_day d = wednesday.
Proof.
  exists tuesday.
  simpl. reflexivity.
Qed.

(*|
## In-class exercise: informal proof

Now let's prove something more interesting: every day has a previous day.

Think-pair-share and come up with an informal proof strategy. Then I'll show how
to translate it to a Rocq proof.
|*)
Lemma every_day_has_prev : forall d, exists d', next_day d' = d.
Proof.
  (* Goal is a forall, so introduce it. *)
  intros d.
(* EXERCISE: Admitted. *)
(* SOLUTION *)
  destruct d.
  - exists sunday. reflexivity.
  - exists monday. reflexivity.
  - exists tuesday. reflexivity.
  - exists wednesday. reflexivity.
  - exists thursday. reflexivity.
  - exists friday. reflexivity.
  - exists saturday. reflexivity.
Qed.
(* /SOLUTION *)


(*| ## Option monad

This section introduces two more core features of functional programming:
polymorphic types (also called "generics" in other languages) and "higher-order
functions" (functions that take other functions as parameters).

|*)

Module Option.

  (*| `option` is a polymorphic type: it takes a type `A` as an argument, and
  (maybe) contains a value of that arbitrary type. `option A` is the simplest
  "container" type. |*)
  Inductive option (A: Type) :=
  | Some (x: A)
  | None.

  (*| Here are some functions you can define on `option`. There are good
  motivations for _why_ you should define these particular ones, but we won't
  get into that (and it isn't all that important for this class). For now, just
  try to understand the behavior. |*)

  (*| `map` runs `f` "inside" the optional value. |*)
  Definition map {A B} (ma: option A) (f: A -> B) : option B :=
    match ma with
    | Some _ x => Some B (f x)
    | None _ => None B
    end.

  (*| Notice the extra type argument we had to provide to `Some`, and the
  somewhat odd `_` in the pattern match. To make it easier to work with
  polymorphic functions, Rocq has a feature called _implicit arguments_. |*)

  (*| These commands modify how type inference treats `Some` and `None`, making
  the type argument implicit (that's what the curly braces mean). Don't worry
  about the syntax; you won't need to do this yourself. |*)
  Arguments Some {A} x.
  Arguments None {A}.

  (*| We'll now define `return_` (it should be called `return` but that's a Rocq
  keyword) and `bind`. These make `option` into a _Monad_ but you don't need
  to understand that, just read the definitions. |*)

  Definition return_ {A} (x: A) : option A := Some x.

  Definition bind {A B} (ma: option A) (f: A -> option B) : option B :=
    match ma with
    | Some x => f x
    | None => None
    end.

  (*| These are some properties of `return_` and `bind` (again, good reason for
  these but not relevant here). |*)

  Lemma return_left_id {A B} (x: A) (f: A -> option B) :
    bind (return_ x) f = f x.
  Proof. reflexivity. Qed.

  Lemma return_right_id {A} (ma: option A) :
    bind ma return_ = ma.
  Proof. destruct ma; reflexivity. Qed.

  Lemma bind_assoc {A B C} (ma: option A) (f: A -> option B) (g: B -> option C) :
    bind (bind ma f) g = bind ma (fun x => bind (f x) g).
  Proof. destruct ma; reflexivity. Qed.

End Option.

(*| ## More proof tactics |*)

Module MoreNatProofs.

Lemma add_0_l n :
  0 + n = n.
Proof.
  simpl. (* Works because [add] pattern matches on the first argument. *)
  reflexivity.
Qed.

(*| The above proof is a "proof by computation" which followed from the
definition of `add`. We'll now go through some "propositional" proofs that
follow from the rules for manipulating logical AND (`∧`) and OR (`∨`). |*)

Lemma O_or_succ n :
  n = 0 \/ n = S (Nat.pred n).
Proof.
  destruct n as [ | n']. (** Make a case distinction on [n]. *)
  - (** Case [n = 0] *)
    left.
    reflexivity.
  - (** Case [n = S n'] *)
    right.
    simpl. (** [pred (S n')] simplifies to [n']. *)
    reflexivity.
Qed.

(*| This proof uses `intros` and `rewrite`.

Rocq allows you to write `intros` without arguments, in which case it will
automatically select names. We strongly recommend in this class to always give
names, since it makes your proof easier to read and modify, as well as making it
easier to read the context while you're developing a proof. |*)
Lemma eq_add_O_2 n m :
  n = 0 -> m = 0 -> n + m = 0.
Proof.
  (** The goal is an implication, and we can "introduce" an hypothesis with the
  [intros] tactic - notice the effect on the goal *)
  intros Hn. (* {GOAL DIFF} *)
  intros Hm.

  (*| `rewrite` is another fundamental proof technique |*)
  rewrite Hn. (* {GOAL DIFF} *)
  rewrite Hm.
  simpl.
  reflexivity.
Qed.

(*| This lemma is a proof of a disequality, a "not equals". Even this isn't
built-in to Rocq but built from simpler primitives. |*)
Lemma neq_succ_0 n :
  S n <> 0.
Proof.
  (* Wade through the sea of notation *)
  Locate "<>". (* {OUTPUT} *)
  Locate "~". (* {OUTPUT} *)
  Print not. (* {OUTPUT} *)
  (** We see that [a <> b] is notation for [not (a = b)], which is by definition
  [a = b -> False]. *)

  unfold not.

  (** Since our goal is an implication, we use [intros]: *)
  intros Hn.

  (** It is impossible for [S ...] to be equal to [0], we can thus derive
  anything, including [False], which is otherwise never provable. The
  [discriminate] tactic looks for an impossible equality and solves any goal by
  contradiction. *)
  discriminate.
Qed.

Lemma succ_pred n : n <> 0 -> n = S (Nat.pred n).
Proof.
  intros Hn.
  destruct (O_or_succ n) as [H0|HS].
  - unfold not in Hn.
    (* There are a few different ways to proceed. Here's one: *)
    exfalso. (* [exfalso] encodes the strategy of proving [False] from the
    current hypotheses, from which the original conclusion follows (regardless
    of what it is). *)
    apply Hn.
    assumption.
  - assumption.
Qed.

End MoreNatProofs.
